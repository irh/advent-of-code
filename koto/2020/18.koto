# https://adventofcode.com/2020/day/18

peekable_tokens = |expression|
  expression
    .chars()
    .keep |c| c != " "
    .peekable()

evaluate_expression = |tokens, ops, min_priority|
  lhs = null
  # Consume tokens until the end of the expression,
  # or an operator with insufficient priority is reached.
  while token = tokens.peek()
    match token.get()
      ')' then
        break
      '(' then
        tokens.next()
        # Parse the nested expression, starting with minimum priority of 0
        lhs = evaluate_expression tokens, ops, 0
        assert_eq tokens.next().get(), ')' # consume the end )
      peeked_op if ops.contains_key peeked_op then
        # Check if the op has sufficient priority to continue
        left_priority, right_priority = ops.get peeked_op
        if left_priority < min_priority
          break
        # Consume the op token and evaluate the rhs
        tokens.next()
        op = peeked_op
        rhs = evaluate_expression tokens, ops, right_priority
        lhs = match op
          '+' then lhs + rhs
          '*' then lhs * rhs
      n then
        tokens.next()
        lhs = n.to_number()
  lhs

evaluate_1 = |input|
  # + and * have the same priority
  ops =
    '*': (1, 2)
    '+': (1, 2)
  tokens = peekable_tokens input
  evaluate_expression tokens, ops, 0

evaluate_2 = |input|
  # + has higher priority than *
  ops =
    '*': (1, 2)
    '+': (3, 4)
  tokens = peekable_tokens input
  evaluate_expression tokens, ops, 0

@main = ||
  input = io.extend_path koto.script_dir(), 'input', '18'
    -> io.read_to_string
  expressions = input.lines().to_tuple()

  part_one = expressions.each(evaluate_1).sum()
  print "Part one: {part_one}" # 45840336521334

  part_two = expressions.each(evaluate_2).sum()
  print "Part two: {part_two}" # 328920644404583

export
  @test part_one: ||
    assert_eq (evaluate_1 "1 + 2 * 3 + 4 * 5 + 6"), 71
    assert_eq (evaluate_1 "1 + (2 * 3) + (4 * (5 + 6))"), 51
    assert_eq (evaluate_1 "((2 + 4 * 9) * (6 + 9 * 8 + 6) + 6) + 2 + 4 * 2"), 13632

  @test part_two: ||
    assert_eq (evaluate_2 "1 + 2 * 3 + 4 * 5 + 6"), 231
    assert_eq (evaluate_2 "1 + (2 * 3) + (4 * (5 + 6))"), 51
    assert_eq (evaluate_2 "((2 + 4 * 9) * (6 + 9 * 8 + 6) + 6) + 2 + 4 * 2"), 23340
